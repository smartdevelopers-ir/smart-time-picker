plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 19
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
}
group = 'ir.smartdevelopers'
version = '1.0.0'
afterEvaluate{
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'ir.smartdevelopers'
                artifactId = 'smart-time-picker'
                pom {
                    name = 'smart-time-picker'
                    description = 'pick file easilly'
                    url = 'https://github.com/smartdevelopers-ir/smart-time-picker'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'smartdevelopers'
                            name = 'mostafa babaei'
                            email = 'info@smartdevelopers.ir'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/smartdevelopers-ir/smart-time-picker.git'
                        developerConnection = 'scm:git:ssh://github.com/smartdevelopers-ir/smart-time-picker.git'
                        url = 'http://github.com/smartdevelopers-ir/smart-time-picker'
                    }

                }
            }
        }
        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

}
task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}
task javadocJar2(type: Jar) {
    archiveClassifier.set( 'javadoc')
    from android.sourceSets.main.java.srcDirs
}
signing {
    sign configurations.archives
}

artifacts {
    archives sourcesJar
    archives javadocJar2
}

